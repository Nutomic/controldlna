buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath 'com.alexvasilkov:android_sign:0.2'
    }
}

apply plugin: 'android_sign'
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url "http://4thline.org/m2"
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:19.1.+'
    compile 'com.android.support:mediarouter-v7:19.1.+'
    compile 'org.fourthline.cling:cling-core:2.1.1'
    compile 'org.fourthline.cling:cling-support:2.1.1'
    compile 'org.eclipse.jetty:jetty-server:8.1.8.v20121106'
    compile 'org.eclipse.jetty:jetty-servlet:8.1.8.v20121106'
    compile 'org.eclipse.jetty:jetty-client:8.1.8.v20121106'
    compile 'org.slf4j:slf4j-jdk14:1.6.1'
}

android {
    compileSdkVersion 19
    buildToolsVersion '25.0.0'

	// Caused by cling, because they apparently include APIs for other platforms.
	lintOptions {
		disable 'InvalidPackage'
	}

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude 'about.html'
    }

    defaultConfig {
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
    }

    signingConfigs {
        release {
            // Android Studio does not pass environment variables.
            // This means you have to use the command line for release builds.
            def ks = System.getenv("KEYSTORE")
            def ka = System.getenv("KEY_ALIAS")
            if (ks != null && ka != null) {
                storeFile file(ks)
                keyAlias ka
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
